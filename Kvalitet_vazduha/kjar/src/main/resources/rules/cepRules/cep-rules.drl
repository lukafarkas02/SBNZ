package rules.cepRules

import com.ftn.model.AirPollutionEvent;
import com.ftn.model.AirQualityStatus;
import com.ftn.model.AirQualityCategory;

declare AirPollutionEvent
    @role( event )
    @timestamp( timestamp )
end

rule "PM2.5 raspon vece od 20 u 2m (DEMO)"
dialect "java"
no-loop true
when
  $max : Number() from accumulate(
           $e : AirPollutionEvent() over window:time(2m),
           max( $e.getPm25() )
         )
  $min : Number() from accumulate(
           $e2 : AirPollutionEvent() over window:time(2m),
           min( $e2.getPm25() )
         )
  eval( $max.doubleValue() - $min.doubleValue() > 20.0 )
  $status : AirQualityStatus()
then
  System.out.println(">> Upozorenje: veliki porast PM2.5 (max-min > 20 u 2m)");
  $status.setExplanation("Razlika PM2.5 u 2 minuta veća od 20 μg/m³ (demo).");
  $status.setRecommendation("Pojačati monitoring i informisati javnost.");
  update($status);
end



rule "Opasan vazduh - demo 2m, bez prekida"
no-loop true
when
    
    Number( intValue >= 4 ) from accumulate(
        $d: AirPollutionEvent( $d.getPm25() >= 55 ) over window:time(2m),
        count($d)
    )
 
    not( $d1: AirPollutionEvent( $d1.getPm25() < 55 ) over window:time(2m) )

    $status : AirQualityStatus()
then
    System.out.println(">> Upozorenje institucijama (DEMO): vazduh je OPASAN neprekidno ≥2min!");
    $status.setCategory(AirQualityCategory.OPASAN);
    $status.setExplanation("PM2.5 ≥ 55 μg/m³ bez prekida u poslednja 2 minuta.");
    $status.setRecommendation("Obavestiti institucije; izbegavati boravak na otvorenom.");
    update($status);
end


rule "Dugotrajno zadržavanje smoga (12h)"
dialect "java"
no-loop true
when

    $minPm25 : Number() from accumulate(
        $e2 : AirPollutionEvent() over window:time(2m),
        min($e2.getPm25())
    )
    eval( $minPm25.doubleValue() > 55.0 )

    $avgWind : Number() from accumulate(
        $e3 : AirPollutionEvent() over window:time(2m),
        average($e3.getWindSpeed())
    )
    eval( $avgWind.doubleValue() < 2.0 )

    $status : AirQualityStatus()
then
    System.out.println(">> Upozorenje: dugotrajno zadržavanje smoga (PM2.5 >55 12h, vetar slab)");
    $status.setExplanation("PM2.5 je kontinuirano iznad 55 µg/m³ u 12h uz slab vetar.");
    $status.setRecommendation("Očekivati zadržavanje smoga; savetovati ograničavanje boravka napolju.");
    update($status);
end